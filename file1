/*ICE Agent Daily Report*/


%include "/pcb/dmt/teamshare/LIB_CRON.sas";

/*libname ICE8PR oracle path="P053" schema= ICE8_PR USER= "INIU"    PASSWORD="F@cility6*";*/

/*%let REPORT_DATE = MDY(4,01,2020);*/
%LET REPORT_DATE= %sysfunc(intnx(day,%sysfunc(today()),-1));

/*%let REPORT_DATE1 = %SYSFUNC(&REPORT_DATE, DATE9.);*/
%LET REPORT_DATE1=%sysfunc(intnx(day,%sysfunc(today()),-1),date9.);



/*Using AGENTNOTREADYBREAKDOWN_D table to get not ready time duration for Coaching and Training and NOTREADYREASONNAME table to find Coaching and Training names*/

proc sql;
create table Not_Ready_Reason_table as 
select 
a.STARTDATETIME
,a.SWITCHID
,a.AGENTID
,a.DURATION
,b.NOTREADYREASONNAME
from ICE8PR.STAT_AGENTNOTREADYBREAKDOWN_D a 
left join ICE8PR.NOTREADYREASONNAME b on a.NOTREADYREASON = b.NOTREADYREASONCODE
where DATEPART(STARTDATETIME) = &REPORT_DATE
and b.NOTREADYREASONNAME in ("Coaching","Training")
/*and a.AGENTID = 2144*/
order by AGENTID,NOTREADYREASONNAME;
quit;


proc sort data=Not_Ready_Reason_table out=Not_Ready_Reason_table;
by STARTDATETIME;
run;


/*Use "by" for the columns you don't want to transpose,*/
/*Use "var" for the columns you would like to transpose,*/

proc transpose data=Not_Ready_Reason_table prefix=NOTREADYTIME out=Not_Ready_Reason_table2  (drop= _NAME_ _LABEL_);
	by STARTDATETIME SWITCHID AGENTID;
	var DURATION;
	id NOTREADYREASONNAME
	;

run;


/*Using Stat_AgentActivity_D table to get agent activities info*/

PROC SQL;
CREATE TABLE agent_activity1 AS
SELECT STARTDATETIME, 
AGENTID, 
LOGONDURATION, 
TOTALREADYTIME, 
TOTALNOTREADYTIME, 
sum(TOTALQUEUECALLTIME, TOTALDIRECTCALLTIME, TOTALOUTBOUNDCALLTIME, TOTALINTERNALCALLTIME, TotalHoldingTimeQueueCalls,
TotalHoldingTimeOtherCalls, TotalConsultTimeQueueCalls, TotalConsultTimeOtherCalls, TotalQueueCallAlertingTime, 
TotalDirectCallAlertingTime, TotalQueueEmailTime, TotalOtherEmailTime, TotalQueueWebChatTime, TotalOtherWebChatTime, TotalWrapUpTime, TotalCallSetupTime) as TOTALCUSTOMERTIME,
sum(TotalConferenceTimeQueueCalls, TotalConferenceTimeOtherCalls,TotalMonitoringTime) as OTHER
FROM ICE8PR.Stat_AgentActivity_D
where DATEPART(STARTDATETIME) = &REPORT_DATE
order by 1,2
;
/*WHERE YEAR(DATEPART(CALLSTARTDATETIME)) = &CALL_YEAR AND MONTH(DATEPART(CALLSTARTDATETIME)) = &CALL_MONTH;*/
QUIT;



proc sql;
create table agent_activity2  as 
select * from agent_activity1 a
left join Not_Ready_Reason_table2 (drop=SWITCHID) b on a.STARTDATETIME = b.STARTDATETIME
													and a.AGENTID = b.AGENTID;
quit;

proc sql;
create table agent_activity3 as 
select *,
case when NOTREADYTIMECoaching = . then 0 else NOTREADYTIMECoaching end as NOTREADYTIME_COACHING,
case when NOTREADYTIMETraining = . then 0 else NOTREADYTIMETraining end as NOTREADYTIME_TRAINING

from agent_activity2;
quit;



data agent_activity4;
set agent_activity3(drop=NOTREADYTIMECoaching NOTREADYTIMETraining);
/*STARTDATETIME2 = datepart(STARTDATETIME)*/

if LOGONDURATION in (0,.) then do;
	READY_TIME_PERCENT = 0; 
	NOTREADY_TIME_PERCENT = 0;
	CUSTOMER_TIME_PERCENT = 0; 
	OTHER_PERCENT = 0;
	end;

else if LOGONDURATION not in (0,.) then do;
	READY_TIME_PERCENT = TOTALREADYTIME / LOGONDURATION;
	NOTREADY_TIME_PERCENT = TOTALNOTREADYTIME/LOGONDURATION;
	CUSTOMER_TIME_PERCENT = TOTALCUSTOMERTIME / LOGONDURATION;
	OTHER_PERCENT = OTHER/LOGONDURATION;
	end;

if TOTALNOTREADYTIME in (0,.) then do;
	NOTREADYTIME_COACHING_PERCENT = 0; 
	NOTREADYTIME_TRAINING_PERCENT = 0;
	end;

else if TOTALNOTREADYTIME not in (0,.) then do;
	NOTREADYTIME_COACHING_PERCENT = NOTREADYTIME_COACHING/TOTALNOTREADYTIME;
	NOTREADYTIME_TRAINING_PERCENT = NOTREADYTIME_TRAINING/TOTALNOTREADYTIME;
	end;

FORMAT READY_TIME_PERCENT percent10.2
NOTREADY_TIME_PERCENT percent10.2
CUSTOMER_TIME_PERCENT percent10.2
OTHER_PERCENT percent10.2
NOTREADYTIME_COACHING_PERCENT percent10.2
NOTREADYTIME_TRAINING_PERCENT percent10.2;
/*STARTDATETIME2 date9.;*/
RUN;


PROC SQL;
CREATE TABLE agent_activity5 AS
SELECT STARTDATETIME, AGENTID, put(LOGONDURATION,time13.) as LOGONDURATION, 
put(TOTALREADYTIME,time13.) as TOTALREADYTIME, 
put(TOTALNOTREADYTIME,time13.) as TOTALNOTREADYTIME, 
put(TOTALCUSTOMERTIME, time13.) as TOTALCUSTOMERTIME,
put(OTHER, time13.) as OTHER,
put(NOTREADYTIME_COACHING, time13.) as NOTREADYTIME_COACHING,
put(NOTREADYTIME_TRAINING, time13.) as NOTREADYTIME_TRAINING,
put(READY_TIME_PERCENT, percent10.2) as READY_TIME_PERCENT, 
put(NOTREADY_TIME_PERCENT, percent10.2) as NOTREADY_TIME_PERCENT, 
put(CUSTOMER_TIME_PERCENT, percent10.2) as CUSTOMER_TIME_PERCENT, 
put(OTHER_PERCENT, percent10.2) as OTHER_PERCENT,
put(NOTREADYTIME_COACHING_PERCENT, percent10.2) as NOTREADYTIME_COACHING_PERCENT,
put(NOTREADYTIME_TRAINING_PERCENT, percent10.2) as NOTREADYTIME_TRAINING_PERCENT
FROM agent_activity4;
/*WHERE YEAR(DATEPART(CALLSTARTDATETIME)) = &CALL_YEAR AND MONTH(DATEPART(CALLSTARTDATETIME)) = &CALL_MONTH;*/
QUIT;



PROC EXPORT DATA= agent_activity5
            OUTFILE = "/techist/DFSPCB04/Personal Folders/Irene Niu/ICE_Agent_Report_&REPORT_DATE1..xlsx"
            DBMS=xlsx REPLACE;
/*     SHEET="Sheet1"; */
RUN;


/*--------------------------------*/
/*		  Summary Table			  */
/*--------------------------------*/

data agent_activity5;
set agent_activity4;
DATE = datepart(STARTDATETIME);
format DATE date9.;
run;

proc sql;
create table summary as 
select
DATE,
count(AGENTID) as AGENTS_COUNT,
/*put(min(LOGONDURATION),time13.) as Min,*/
/*put(max(LOGONDURATION),time13.) as Max,*/
put(avg(LOGONDURATION),time13.) as AVG_LOGONDURATION,
put(avg(TOTALREADYTIME),time13.) as AVG_TOTALREADYTIME,
put(avg(TOTALNOTREADYTIME),time13.) as AVG_TOTALNOTREADYTIME,
put(avg(TOTALCUSTOMERTIME),time13.) as AVG_TOTALCUSTOMERTIME,
put(avg(OTHER),time13.) as AVG_OTHER,
put(avg(NOTREADYTIME_COACHING),time13.) as AVG_NOTREADYTIME_COACHING,
put(avg(NOTREADYTIME_TRAINING),time13.) as AVG_NOTREADYTIME_TRAINING,


put(avg(READY_TIME_PERCENT),percent10.2) as AVG_READY_TIME_PERCENT,
put(avg(NOTREADY_TIME_PERCENT),percent10.2) as AVG_NOTREADY_TIME_PERCENT,
put(avg(CUSTOMER_TIME_PERCENT),percent10.2) as AVG_CUSTOMER_TIME_PERCENT,
put(avg(OTHER_PERCENT),percent10.2) as AVG_OTHER_PERCENT,
put(avg(NOTREADYTIME_COACHING_PERCENT),percent10.2) as AVG_NOTREADYTIME_COACH_PERCENT,
put(avg(NOTREADYTIME_TRAINING_PERCENT),percent10.2) as AVG_NOTREADYTIME_TRAIN_PERCENT


from agent_activity5
group by 1;
quit;

data summary2;
set summary;
label 
	DATE = Date
	AGENTS_COUNT = Number of Agents
	AVG_LOGONDURATION = AVG Logon Duration 	
	AVG_TOTALREADYTIME	= AVG Total Ready Time	
	AVG_TOTALNOTREADYTIME = AVG Total Not Ready Time
	AVG_TOTALCUSTOMERTIME = AVG Total Cutomer Time
	AVG_OTHER = AVG Other
	AVG_NOTREADYTIME_Coaching = AVG Not Ready Time Coaching
	AVG_NOTREADYTIME_Training = AVG Not Ready Time Training
	AVG_READY_TIME_PERCENT = AVG Ready Time %
	AVG_NOTREADY_TIME_PERCENT = AVG Not Ready Time %
	AVG_CUSTOMER_TIME_PERCENT = AVG Customer Time %
	AVG_OTHER_PERCENT = AVG Other %
	AVG_NOTREADYTIME_COACH_PERCENT =  AVG Not Ready Time Coaching %
	AVG_NOTREADYTIME_TRAIN_PERCENT = AVG Not Ready Time Training %
;

run;



/*--------------------------------*/
/*		  Sending Email			  */
/*--------------------------------*/

option emailhost = lclmxbh01;
               filename mailbox email
               to = ("Sudesh.Sundraiya@pcbank.ca" "Jack.Ye@pcbank.ca" "Shawn.Reid@pcbank.ca"  )
               cc = ("LORIN.GRINER@pcbank.ca" "Romil.Dharia@pcbank.ca" "Nima.Etezazian@pcbank.ca" "Irene.Niu@mtcad.onmicrosoft.com" "PCFDataGovernance@pcbank.ca")
/*                to = ("Nima.Etezazian@pcbank.ca") */


                   subject= "ICE Agent Report for &REPORT_DATE1 is ready" 
               from = "Nima.Etezazian@pcbank.ca" 

                   attach=("/techist/DFSPCB04/Personal Folders/Irene Niu/ICE_Agent_Report_&REPORT_DATE1..xlsx"  content_type="application/xlsx")
               type = "text/html"
               CT = "text/html";
               
               ods html body = mailbox;
               ods html text='<div align="left">Hi All,</div>';
               ods html text='<div align="left"></div>';
               ods html text='<div align="left">  A Call Centre Agent Report has been generated. Please find the file attached. </div>';
                   ods html text='<div align="left">  Please let me know if you have any questions. </div>';
                
               ods html text='<div align="left">Nima</div>';
               ods html text='<div align="left"> </div>';
               ods html text='<div align="left"></div>';

                   title;
				ods html text='<div align="center">Summary Table</div>';
			   
				proc print data= summary2 noobs label style =[background=white borderwidth=1 ];  
			
				run;
               
run;


